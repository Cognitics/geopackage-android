apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group = "mil.nga.geopackage"
archivesBaseName = "geopackage-android"
version = "1.1.0"
def remotePublish = false

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21

        testApplicationId 'mil.nga.geopackage.test'
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
        }

        lintOptions {
            abortOnError false
        }
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        destinationDir = file("../javadoc/")
        failOnError false
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.java.srcDirs
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        required {remotePublish}
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {

                if (!remotePublish) {

                    repository url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository')

                } else {

                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    pom.project {
                        name 'GeoPackage Android'
                        packaging 'aar'
                        description 'GeoPackage Android implementation'
                        url 'https://github.com/ngageoint/geopackage-android'

                        scm {
                            url 'git@github.com:ngageoint/geopackage-android.git'
                            connection 'scm:git:git@github.com:ngageoint/geopackage-android.git'
                            developerConnection 'scm:git:git@github.com:ngageoint/geopackage-android.git'
                        }

                        licenses {
                            license {
                                name 'The MIT License (MIT)'
                                url 'https://github.com/ngageoint/geopackage-android/blob/master/LICENSE.txt'
                                distribution 'repo'
                            }
                        }

                        organization {
                            name 'National Geospatial-Intelligence Agency'
                            url 'https://www.nga.mil'
                        }

                        developers {
                            developer {
                                id 'bosborn'
                                name 'Brian Osborn'
                                email 'osbornb@bit-sys.com'
                                organizationUrl 'https://www.bit-sys.com'
                                roles {
                                    role 'developer'
                                }
                                timezone 'UTC-07'
                            }
                        }
                    }
                }
            }
        }
    }

}

dependencies {
    compile 'com.android.support:support-v4:21.0.2'
    compile 'com.google.android.gms:play-services:6.1.71'
    compile 'com.google.maps.android:android-maps-utils:0.3.3'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'mil.nga.geopackage:geopackage-core:1.1.0'
}
